---
title: "Understanding Basic GLSL Shader Techniques"
date: 2021-05-07
author: Tom
slug: understanding-basic-glsl-shader-techniques
---

There are some excellent resources online for learning shaders and WebGL, namely
[The Book of Shaders](https://thebookofshaders.com) and
[WebGL Fundamentals](https://webglfundamentals.org). This post covers some of
the basic patterns that I needed to learn in order to complete
[the Dream Builder's crop implementation](/blog/implementing-crop).

# Mix a Mask

```cpp
float mask = step(edge, x);
vec3 color = mix(color1, color2, mask);
```

The code for each shape below can be abstracted to a separate function and
called from main like so:

```cpp
vec3 color = mix(color1, color2, getShapeMask(v));
```

# Draw a Rectangle

```cpp
varying vec2 vUv;

float rect(vec2 xy, vec2 wh, vec2 st) {
  vec2 xyMask = step(xy, st);
  vec2 whMask = 1.0 - step(xy + wh, st);
  vec2 mask = xyMask * whMask;
  return mask.x * mask.y;
}

void main() {
    vec4 red = vec4(1.,0.,0.,1.);
    vec4 blue = vec4(0.,0.,1.,1.);

    vec2 xy = vec2(.1,.2);
    vec2 wh = vec2(.3,.4);

    gl_FragColor = mix(blue, red, rect(xy, wh, vUv));
}
```

# Step Between Macro

This is used to return `1.0` if `v` is between `lo` and `hi`, `0.0` otherwise:

```cpp
#define between(lo, hi, v) step(lo, v) * step(v, hi)
```

This logic comes in very handy for the subsequent shapes.

# Rectangle with Borders

**Notice how `max()` and `min()` act as union/intersection** on our mask in this
example:

```cpp
float outlineRect(vec2 xy, vec2 wh, vec2 th, vec2 st) {
  vec2 bl = between(xy, xy + th, st);
  vec2 tr = between(xy + wh, xy + wh + th, st);
  float l = min(bl.x, between(xy.y, xy.y + wh.y + th.y, st.y));
  float b = min(bl.y, between(xy.x, xy.x + wh.x + th.x, st.x));
  float r = min(tr.x, between(xy.y, xy.y + wh.y + th.y, st.y));
  float t = min(tr.y, between(xy.x, xy.x + wh.x + th.x, st.x));
  return max(max(l, b), max(r, t));
}
```

# Full Border

Naively we can use our `between` macro again:

```cpp
float full_border(vec2 th, vec2 st) {
  vec2 mask = between(th, 1.0 - th, st);
  return 1.0 - min(mask.x, mask.y);
}
```

But if you scale this up and down (see below) you'll notice the border continues
infinitely beyond the edges of the clip space. It may be more desirable to use
our `outlineRect` again to produce a finite border:

```cpp
float full_border(vec2 th, vec2 st) {
  return outlineRect(vec2(0.0), 1.0 - th, th, st);
}
```

# Circle

A simple circle:

```cpp
float circle(float radius, vec2 st) {
  vec2 center = vec2(0.5, 0.5);
  float size = radius;
  return 1.0 - step(size, distance(center, st));
}
```

# Shape Cutting

We can cut one mask with another. Here we cut a circle out of a full border
which creates the effect of pointed arrow corners, ideal for scaling/resizing
handles:

```cpp
// these values produce the desired effect
vec2 border_thickness = vec2(0.025);
float radius = 0.6;

// shape cutting code
float border_mask = full_border(border_thickness, st);
vec4 color = mix(bg_color, border_color, border_mask);
gl_FragColor = mix(color, bg_color, min(border_mask, circle(radius, st)));
```

# More from this series

This post is part of the [Dream Builder Series](/blog/dream-builder-series).

The previous post was
[Setting up Shaders with React Three Fiber](/blog/react-three-fiber-shaders-setup).

The next post is [Implementing Crop](/blog/implementing-crop).

# Get in touch

**If you have any questions or ideas, please email me at
[tom@bearjam.dev](mailto:tom@bearjam.dev).**

<!-- ## Maybe this goes in the crop post

```cpp
uniform vec4 u_inset;
uniform float u_handle_length;

float rect(vec2 xy, vec2 wh, vec2 st) {
  vec2 mask = step(xy, st);
  mask *= 1.0 - step(xy + wh, st);
  return mask.x * mask.y;
}

mat2 scale(vec2 _scale) { return mat2(_scale.x, 0.0, 0.0, _scale.y); }

float top(vec2 st, vec2 th) {
  vec2 xy = vec2(0.0, 1.0 - th.y);
  vec2 wh = vec2(1.0, th.y);
  wh *= scale(vec2(u_handle_length, 1.0)) *
        scale(vec2(1.0 - (u_inset.y + u_inset.w), 1.0));
  xy += vec2((wh.x / 2.0) + u_inset.w, -u_inset.x);
  return rect(xy, wh, st);
}

float right(vec2 st, vec2 th) {
  vec2 xy = vec2(1.0 - th.x, 0.0);
  vec2 wh = vec2(th.x, 1.0);
  wh *= scale(vec2(1.0, u_handle_length)) *
        scale(vec2(1.0, 1.0 - (u_inset.x + u_inset.z)));
  xy += vec2(-u_inset.y, (wh.y / 2.0) + u_inset.z);
  return rect(xy, wh, st);
}

float bottom(vec2 st, vec2 th) {
  vec2 xy = vec2(0.0, 0.0);
  vec2 wh = vec2(1.0, th.y);
  wh *= scale(vec2(u_handle_length, 1.0)) *
        scale(vec2(1.0 - (u_inset.y + u_inset.w), 1.0));
  xy += vec2((wh.x / 2.0) + u_inset.w, u_inset.z);
  return rect(xy, wh, st);
}

float left(vec2 st, vec2 th) {
  vec2 xy = vec2(0.0, 0.0);
  vec2 wh = vec2(th.x, 1.0);
  wh *= scale(vec2(1.0, u_handle_length)) *
        scale(vec2(1.0, 1.0 - (u_inset.x + u_inset.z)));
  xy += vec2(u_inset.w, (wh.y / 2.0) + u_inset.z);
  return rect(xy, wh, st);
}
```

## RTS Matrices

Remember, you're operating on the underlying UV coordinates. So if you want to
scale your shapes up, you need to scale the UV down. If you want to rotate your
shape at (x,y), you need to

### Indexing Fracted Grids

```cpp
float factor = 3.0;
vec2 st = vUv * factor;
int x = int(floor(st.x));
int y = int(floor(st.y));
float shape = ttt[y][x];
st = fract(st);
```

It's important that the `fract(st)` line come after, not before, the indexing

### Scale Shape Coords?

This seems to work

```cpp
float top(vec2 st) {
  vec2 xy = vec2(0.0, 1.0 - u_border_thickness);
  vec2 wh = vec2(1.0, u_border_thickness);
  vec2 uv = st;
  wh *=
      scale(vec2(u_handle_length, 1.0)) * scale(vec2(1.0 - (u_inset.y + u_inset.w), 1.0));
  xy += vec2((wh.x / 2.0) + u_inset.w, -u_inset.x);
  return rect(xy, wh, uv);
}
```

I'm operating on a copy of the UV coords and then returning a mask, leaving the
actual UV untouched

### Scale

```cpp
mat2 scale(vec2 _scale){
  return mat2(_scale.x, 0.0, 0.0, _scale.y);
}
``` -->
